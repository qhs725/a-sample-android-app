import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            keyAlias 'utc_vat'
            keyPassword 'utcandroid'
            storeFile file(projectDir.getParent() + '/utc-vat.keystore')
            storePassword 'utcandroid'
        }
    }

    compileSdkVersion 'Google Inc.:Google APIs:23'
    buildToolsVersion '23.0.1'

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "edu.utc.vat"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
            jniDebuggable false
        }
        debug {
            jniDebuggable true
        }
    }
    sourceSets {
        main {
            jniLibs.srcDir 'src/main/libs'
            jni.srcDirs = [] //disable automatic ndk-build
        }
    }
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkBuild
}

task ndkBuild(type: Exec) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkbuild = properties.getProperty('ndk.dir', null) + "/ndk-build"
    def ndkbuildwin = properties.getProperty('ndk.dir', null) + "\\ndk-build.cmd"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine ndkbuildwin, '-C', file('src/main/jni').absolutePath
    } else {
        commandLine ndkbuild, '-C', file('src/main/jni').absolutePath
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // compile 'com.google.http-client:google-http-client-gson:1.20.0'
    //compile 'com.google.http-client:google-http-client:1.20.0'
    compile('com.google.api-client:google-api-client:1.20.0') {
        exclude module: 'httpcore'
        exclude module: 'httpclient'
    }
    compile('com.google.http-client:google-http-client-jackson:1.20.0') {
        exclude module: 'httpcore'
        exclude module: 'httpclient'
    }
    compile('com.google.oauth-client:google-oauth-client:1.20.0') {
        exclude module: 'httpcore'
        exclude module: 'httpclient'
    }
    compile 'com.google.android.gms:play-services:8.1.0'
    compile 'com.google.android.gms:play-services-gcm:8.1.0'
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.13'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.github.nkzawa:socket.io-client:0.6.0'
    compile 'com.android.support:recyclerview-v7:23.0.0'
    compile 'com.android.support:cardview-v7:23.0.0'
}
